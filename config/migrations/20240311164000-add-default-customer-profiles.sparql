PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX crm: <http://data.rollvolet.be/vocabularies/crm/>
PREFIX schema: <http://schema.org/>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX p2poPrice: <https://purl.org/p2p-o/price#>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?customer crm:hasCustomerProfile ?profile .
    ?profile a crm:CustomerProfile ;
      mu:uuid ?uuid ;
      crm:invoicePaymentPeriod "14"^^xsd:integer ;
      crm:requiresDeposit ?requiresDeposit ;
      p2poPrice:hasVATCategoryCode ?vatRate ;
      schema:deliveryMethod ?deliveryMethod .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?customer a vcard:VCard .
    {
      SELECT ?customer (MAX(?idNumber) as ?id) (STRUUID() AS ?uuid) WHERE {
        ?case schema:customer ?customer ;
        generiek:gestructureerdeIdentificator ?identifier .
        ?identifier generiek:lokaleIdentificator ?idNumber ;
        generiek:naamruimte "AD" .
      } GROUP BY ?customer
    }

    ?case generiek:gestructureerdeIdentificator ?identifier .
    ?identifier generiek:lokaleIdentificator ?id ;
    generiek:naamruimte "AD" .
    OPTIONAL {
      ?case crm:requiresDeposit ?requiresDeposit .
    }
    OPTIONAL {
      ?case p2poPrice:hasVATCategoryCode ?vatRate .
    }
    OPTIONAL {
      ?case schema:deliveryMethod ?deliveryMethod .
    }

    BIND(IRI(CONCAT("http://data.rollvolet.be/customer-profiles/", ?uuid)) as ?profile)
  }
}

;

INSERT {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?customer crm:hasCustomerProfile ?profile .
    ?profile a crm:CustomerProfile ;
    mu:uuid ?uuid ;
    crm:invoicePaymentPeriod "14"^^xsd:integer .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?customer a vcard:VCard .
    {
      SELECT ?customer (STRUUID() AS ?uuid) WHERE {
        ?customer a vcard:VCard .
        FILTER NOT EXISTS {
          ?customer crm:hasCustomerProfile ?profile .
        }
      }
    }
    BIND(IRI(CONCAT("http://data.rollvolet.be/customer-profiles/", ?uuid)) as ?profile)
  }
}

;

INSERT {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?profile ?prop ?default .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/rollvolet> {
    ?profile a crm:CustomerProfile .
    VALUES (?prop ?default) {
      (crm:requiresDeposit "true"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean>)
      (p2poPrice:hasVATCategoryCode <http://data.rollvolet.be/vat-rates/93c1ba2d-3226-441f-bb65-fc498f420a56>)
      (schema:deliveryMethod <http://data.rollvolet.be/concepts/89db0214-65d1-444d-9c19-a0e772113b8a>)
    }
    FILTER NOT EXISTS {
      ?profile ?prop ?value .
    }
  }
}
